# 选择生成器和交叉编译

本章主要内容有：

- 使用 Visual Studio 2017 构建 CMake 项目
- 交叉编译 hello world 示例
- 使用 OpenMP 并行化交叉编译 Windows 二进制文件

CMake 本身不构建可执行程序和库。不过，CMake 配置一个项目，并生成构建工具或框架用于构建项目的文件。在 GNU/Linux 和 macOS 上，CMake 通常生成 Unix Makefile(也存在替代方式)。在 Windows 上，通常生成 Visual Studio 项目文件或 MinGW 或 MSYS Makefile。CMake 包括本地构建工具或集成开发环境(IDE)的生成器。可以通过以下链接阅读更多关于它们的信息:https://cmake.org/cmake/help/latest/manual/cmake-generators.7.html

可以使用`cmake -G`的方式来选择生成器：

```
$ cmake -G "Visual Studio 15 2017"
```

不是每个平台上所有的生成器都可用，而且 CMake 在运行时获取平台信息。要查看当前平台上所有可用生成器的列表，请键入以下命令：

```
$ cmake -G
```

本章中，我们不会使用所有生成器，但是本书中的大多数示例都使用了 Unix Makefile、MSYS Makefile、Ninja 和 Visual Studio 15 2017 进行了测试。

我们将重点讨论 Windows 平台上的开发，将演示不使用命令行，如何使用 Visual Studio 15 2017 直接构建 CMake 项目。还会讨论如何在 Linux 或 macOS 系统上，交叉编译 Windows 的可执行文件。

# 13.1 使用 CMake 构建 Visual Studio 2017 项目

**NOTE**:_此示例代码可以在 codes/chapter-13/recipe-01 中找到，其中包含一个 C++示例。该示例在 CMake 3.5 版(或更高版本)中是有效的，并且已经在 GNU/Linux、macOS 和 Windows 上进行过测试。_

早期版本的 Visual Studio 要求开发人员在不同的 Windows 版本中编辑源代码并运行 CMake 命令，但 Visual Studio 2017 引入了对 CMake 项目的内置支持( https://aka.ms/cmake )，它允许在同一个 IDE 中发生整个编码、配置、构建和测试工作流。本示例中，不需要使用命令行，我们将直接使用 Visual Studio 2017 构建一个简单的“hello world”CMake 示例项目。

## 准备工作

首先，下载并安装 Visual Studio Community 2017 (https://www.visualstudio.com/downloads/ )。在撰写本文时，这个版本是免费的，有 30 天的试用期。我们将遵循的视频中的步骤:https://www.youtube.com/watch?v=_lKxJjV8r3Y

运行安装程序时，在左侧面板上选择`Desktop development with C++`，并在右侧的 Summary 面板上选择用于 CMake 的 Visual C++工具：

sual Studio 2017 15.4 中，还可以为 Linux 平台进行构建。为此，在工具集中选择`Linux development with C++`:

选择后，只要配置 Linux 服务器的访问权限，就可以从 Visual Studio 中同时对 Windows 和 Linux 机器进行构建。我们不在本章中演示这种方法。

这个示例中，我们将在 Windows 上构建一个二进制文件，我们的目标是配置和构建以下示例代码(`hello-world.cpp`)：

```
#include <cstdlib>
#include <iostream>
#include <string>
const std::string cmake_system_name = SYSTEM_NAME;
int main() {
  std::cout << "Hello from " << cmake_system_name << std::endl;

  return EXIT_SUCCESS;
}
```

## 具体实施

创建相应的源码：

1. 创建一个目录，并将`hello-world.cpp`放在新目录中。

2. 目录中，创建一个`CMakeLists.txt`文件，其内容为:

   ```
   # set minimum cmake version
   cmake_minimum_required(VERSION 3.5 FATAL_ERROR)

   # project name and language
   project(recipe-01 LANGUAGES CXX)

   set(CMAKE_CXX_STANDARD 11)
   set(CMAKE_CXX_EXTENSIONS OFF)
   set(CMAKE_CXX_STANDARD_REQUIRED ON)

   include(GNUInstallDirs)
   set(CMAKE_ARCHIVE_OUTPUT_DIRECTORY
     ${CMAKE_BINARY_DIR}/${CMAKE_INSTALL_LIBDIR})
   set(CMAKE_LIBRARY_OUTPUT_DIRECTORY
     ${CMAKE_BINARY_DIR}/${CMAKE_INSTALL_LIBDIR})
   set(CMAKE_RUNTIME_OUTPUT_DIRECTORY
     ${CMAKE_BINARY_DIR}/${CMAKE_INSTALL_BINDIR})

   # define executable and its source file
   add_executable(hello-world hello-world.cpp)

   # we will print the system name in the code
   target_compile_definitions(hello-world
     PUBLIC
       "SYSTEM_NAME=\"${CMAKE_SYSTEM_NAME}\""
     )

   install(
     TARGETS
       hello-world
     DESTINATION
       ${CMAKE_INSTALL_BINDIR}
     )
   ```

3. 打开 Visual Studio 2017，然后通过下面的`File ->Open -> Folder`，选择到新创建的包含源文件和`CMakeLists.txt`的文件夹下。

4. 打开文件夹后，请注意 CMake 配置步骤是如何运行的(面板底部)：

5. 现在，可以右键单击`CMakeLists.txt`(右面板)，并选择`Build`:

6. 构建项目(参见底部面板上的输出):

7. 这就成功地编译了可执行文件。下一小节中，我们将学习如何定位可执行文件，并更改构建和安装路径。

## 工作原理

我们已经看到 Visual Studio 2017 能很好地对接 CMake，并且已经能够在 IDE 中配置和构建代码。除了构建步骤之外，还可以运行安装或测试步骤。可以通过右键单击`CMakeLists.txt`(右面板)，访问这些文件。

然而，配置步骤是自动运行的，我们可能更想去修改配置选项。我们还想知道实际的构建和安装路径，以便测试我们的可执行文件。为此，我们可以选择`CMake -> Change CMake Settings`，如下图所示:

面板左上角，可以检查和修改生成器(本例中是 Ninja)、设置、参数以及路径。构建路径在前面的显示中可以看到。这些设置被分组为构建类型(`x86-Debug`、`x86-Release`等等)，我们可以在面板栏顶部的中间部分，通过选择切换构建类型。

现在知道了构建路径，可以测试编译后的可执行文件:

```
$ ./hello-world.exe

Hello from Windows
```

当然，构建和安装路径可以进行修改。

## 更多信息

- Visual Studio 支持 CMake: https://aka.ms/cmake
- 使用 CMake，基于 Visual C++开发 Linux 应用：https://blogs.msdn.microsoft.com/vcblog/2017/08/25/visual-c-for-linux-development-with-cmake/
- Visual Studio 官方文档：https://www.visualstudio.com/vs/features/ide/

# 13.2 交叉编译 hello world 示例

**NOTE**:_此示例代码可以在 codes/chapter-13/recipe-01 中找到，其中包含一个 C++示例。该示例在 CMake 3.5 版(或更高版本)中是有效的，并且已经在 GNU/Linux、macOS 和 Windows 上进行过测试。_

这个示例中，我们将重用“Hello World”示例，并将代码从 Linux 或 macOS 交叉编译到 Windows。换句话说，我们将在 Linux 或 macOS 上配置和编译代码，并生成 Windows 平台的可执行文件

## 准备工作

我们从`hello world`示例(`hello-world.cpp`)开始：

```
#include <iostream>
#include <omp.h>
#include <string>

int main(int argc, char *argv[])
{
  std::cout << "number of available processors: " << omp_get_num_procs()
            << std::endl;
  std::cout << "number of threads: " << omp_get_max_threads() << std::endl;

  auto n = std::stol(argv[1]);
  std::cout << "we will form sum of numbers from 1 to " << n << std::endl;

  // start timer
  auto t0 = omp_get_wtime();

  auto s = 0LL;
#pragma omp parallel for reduction(+ : s)
  for (auto i = 1; i <= n; i++)
  {
    s += i;
  }
  // stop timer
  auto t1 = omp_get_wtime();

  std::cout << "sum: " << s << std::endl;
  std::cout << "elapsed wall clock time: " << t1 - t0 << " seconds" << std::endl;

  return 0;
}
```

我们还将使用与前一个示例相同的`CMakeLists.txt`：

```
# set minimum cmake version
cmake_minimum_required(VERSION 3.5 FATAL_ERROR)

# project name and language
project(recipe-01 LANGUAGES CXX)

set(CMAKE_CXX_STANDARD 11)
set(CMAKE_CXX_EXTENSIONS OFF)
set(CMAKE_CXX_STANDARD_REQUIRED ON)

include(GNUInstallDirs)
set(CMAKE_ARCHIVE_OUTPUT_DIRECTORY
  ${CMAKE_BINARY_DIR}/${CMAKE_INSTALL_LIBDIR})
set(CMAKE_LIBRARY_OUTPUT_DIRECTORY
  ${CMAKE_BINARY_DIR}/${CMAKE_INSTALL_LIBDIR})
set(CMAKE_RUNTIME_OUTPUT_DIRECTORY
  ${CMAKE_BINARY_DIR}/${CMAKE_INSTALL_BINDIR})

# define executable and its source file
add_executable(hello-world hello-world.cpp)

# we will print the system name in the code
target_compile_definitions(hello-world
  PUBLIC
    "SYSTEM_NAME=\"${CMAKE_SYSTEM_NAME}\""
  )

install(
  TARGETS
    hello-world
  DESTINATION
    ${CMAKE_INSTALL_BINDIR}
  )
```

为了交叉编译源代码，我们需要安装一个 C++交叉编译器，也可以为 C 和 Fortran 安装一个交叉编译器。可以使用打包的 MinGW 编译器，作为打包的交叉编译器的替代方案。还可以使用 MXE (M cross environment)从源代码构建一套交叉编译器：[http://mxe.cc](http://mxe.cc/)

## 具体实施

我们将按照以下步骤，在这个交叉编译的“hello world”示例中创建三个文件:

1. 创建一个文件夹，其中包括`hello-world.cpp`和`CMakeLists.txt`。

2. 再创建一个`toolchain.cmake`文件，其内容为：

   ```
   # the name of the target operating system
   set(CMAKE_SYSTEM_NAME Windows)

   # which compilers to use
   set(CMAKE_CXX_COMPILER i686-w64-mingw32-g++)

   # adjust the default behaviour of the find commands:
   # search headers and libraries in the target environment
   set(CMAKE_FIND_ROOT_PATH_MODE_INCLUDE ONLY)
   set(CMAKE_FIND_ROOT_PATH_MODE_LIBRARY ONLY)

   # search programs in the host environment
   set(CMAKE_FIND_ROOT_PATH_MODE_PROGRAM NEVER)
   ```

3. 将`CMAKE_CXX_COMPILER`设置为对应的编译器(路径)。

4. 然后，通过将`CMAKE_TOOLCHAIN_FILE`指向工具链文件，从而配置代码(本例中，使用了从源代码构建的 MXE 编译器):

   ```
   $ mkdir -p build
   $ cd build
   $ cmake -D CMAKE_TOOLCHAIN_FILE=toolchain.cmake ..

   -- The CXX compiler identification is GNU 5.4.0
   -- Check for working CXX compiler: /home/user/mxe/usr/bin/i686-w64-mingw32.static-g++
   -- Check for working CXX compiler: /home/user/mxe/usr/bin/i686-w64-mingw32.static-g++ -- works
   -- Detecting CXX compiler ABI info
   -- Detecting CXX compiler ABI info - done
   -- Detecting CXX compile features
   -- Detecting CXX compile features - done
   -- Configuring done
   -- Generating done
   -- Build files have been written to: /home/user/cmake-recipes/chapter-13/recipe-01/cxx-example/build
   ```

5. 现在，构建可执行文件：

   ```
   $ cmake --build .

   Scanning dependencies of target hello-world
   [ 50%] Building CXX object CMakeFiles/hello-world.dir/hello-world.cpp.obj
   [100%] Linking CXX executable bin/hello-world.exe
   [100%] Built target hello-world
   ```

6. 注意，我们已经在 Linux 上获得`hello-world.exe`。将二进制文件复制到 Windows 上。

7. 在 WIndows 上可以看到如下的输出：

   ```
   Hello from Windows
   ```

8. 如你所见，这个二进制可以在 Windows 下工作。

## 工作原理

由于与目标环境(Windows)不同的主机环境(在本例中是 GNU/Linux 或 macOS)上配置和构建代码，所以我们需要向 CMake 提供关于目标环境的信息，这些信息记录在`toolchain.cmake`文件中( https://cmake.org/cmake/help/latest/manual/cmake-toolchains.7.html#cross-compiling )。

首先，提供目标操作系统的名称:

```
set(CMAKE_SYSTEM_NAME Windows)
```

然后，指定编译器：

```
set(CMAKE_C_COMPILER i686-w64-mingw32-gcc)
set(CMAKE_CXX_COMPILER i686-w64-mingw32-g++)
set(CMAKE_Fortran_COMPILER i686-w64-mingw32-gfortran)
```

这个例子中，我们不需要检测任何库或头文件。如果必要的话，我们将使用以下命令指定根路径:

```
set(CMAKE_FIND_ROOT_PATH /path/to/target/environment)
```

例如，提供 MXE 编译器的安装路径。

最后，调整`find`命令的默认行为。我们指示 CMake 在目标环境中查找头文件和库文件:

```
set(CMAKE_FIND_ROOT_PATH_MODE_INCLUDE ONLY)
set(CMAKE_FIND_ROOT_PATH_MODE_LIBRARY ONLY)
```

在主机环境中的搜索程序：

```
set(CMAKE_FIND_ROOT_PATH_MODE_PROGRAM NEVER)
```

## 更多信息

有关各种选项的更详细讨论，请参见： https://cmake.org/cmake/help/latest/manual/cmake-toolchains.7.html#cross-compiling

# 13.3 使用 OpenMP 并行化交叉编译 Windows 二进制文件

**NOTE**:_此示例代码可以在 codes/chapter-13/recipe-02 中找到，其中包含一个 C++示例和 Fortran 示例。该示例在 CMake 3.5 版(或更高版本)中是有效的，并且已经在 GNU/Linux、macOS 和 Windows 上进行过测试。_

在这个示例中，我们将交叉编译一个 OpenMP 并行化的 Windows 二进制文件。

## 准备工作

我们将使用第 3 章第 5 节中的未修改的源代码，示例代码将所有自然数加到 N (`example.cpp`):

```
#include <iostream>
#include <omp.h>
#include <string>

int main(int argc, char *argv[]) {
  std::cout << "number of available processors: " << omp_get_num_procs()
  << std::endl;
  std::cout << "number of threads: " << omp_get_max_threads() << std::endl;

  auto n = std::stol(argv[1]);
  std::cout << "we will form sum of numbers from 1 to " << n << std::endl;

  // start timer
  auto t0 = omp_get_wtime();

  auto s = 0LL;
  #pragma omp parallel for reduction(+ : s)
  for (auto i = 1; i <= n; i++) {
  	s += i;
  }

  // stop timer
  auto t1 = omp_get_wtime();

  std::cout << "sum: " << s << std::endl;
  std::cout << "elapsed wall clock time: " << t1 - t0 << " seconds" << std::endl;

  return 0;
}
```

`CMakeLists.txt`检测 OpenMP 并行环境方面基本没有变化，除了有一个额外的安装目标:

```
# set minimum cmake version
cmake_minimum_required(VERSION 3.9 FATAL_ERROR)

# project name and language
project(recipe-02 LANGUAGES CXX)

set(CMAKE_CXX_STANDARD 11)
set(CMAKE_CXX_EXTENSIONS OFF)
set(CMAKE_CXX_STANDARD_REQUIRED ON)

include(GNUInstallDirs)
set(CMAKE_ARCHIVE_OUTPUT_DIRECTORY
	${CMAKE_BINARY_DIR}/${CMAKE_INSTALL_LIBDIR})
set(CMAKE_LIBRARY_OUTPUT_DIRECTORY
	${CMAKE_BINARY_DIR}/${CMAKE_INSTALL_LIBDIR})
set(CMAKE_RUNTIME_OUTPUT_DIRECTORY
	${CMAKE_BINARY_DIR}/${CMAKE_INSTALL_BINDIR})

find_package(OpenMP REQUIRED)

add_executable(example example.cpp)

target_link_libraries(example
  PUBLIC
  	OpenMP::OpenMP_CXX
  )

install(
  TARGETS
  	example
  DESTINATION
  	${CMAKE_INSTALL_BINDIR}
  )
```

## 具体实施

通过以下步骤，我们将设法交叉编译一个 OpenMP 并行化的 Windows 可执行文件:

1. 创建一个包含`example.cpp`和`CMakeLists.txt`的目录。

2. 我们将使用与之前例子相同的`toolchain.cmake`:

   ```
   # the name of the target operating system
   set(CMAKE_SYSTEM_NAME Windows)

   # which compilers to use
   set(CMAKE_CXX_COMPILER i686-w64-mingw32-g++)

   # adjust the default behaviour of the find commands:
   # search headers and libraries in the target environment
   set(CMAKE_FIND_ROOT_PATH_MODE_INCLUDE ONLY)
   set(CMAKE_FIND_ROOT_PATH_MODE_LIBRARY ONLY)
   # search programs in the host environment
   set(CMAKE_FIND_ROOT_PATH_MODE_PROGRAM NEVER)
   ```

3. 将`CMAKE_CXX_COMPILER`设置为对应的编译器(路径)。

4. 然后，通过`CMAKE_TOOLCHAIN_FILE`指向工具链文件来配置代码(本例中，使用了从源代码构建的 MXE 编译器):

   ```
   $ mkdir -p build
   $ cd build
   $ cmake -D CMAKE_TOOLCHAIN_FILE=toolchain.cmake ..

   -- The CXX compiler identification is GNU 5.4.0
   -- Check for working CXX compiler: /home/user/mxe/usr/bin/i686-w64-mingw32.static-g++
   -- Check for working CXX compiler: /home/user/mxe/usr/bin/i686-w64-mingw32.static-g++ -- works
   -- Detecting CXX compiler ABI info
   -- Detecting CXX compiler ABI info - done
   -- Detecting CXX compile features
   -- Detecting CXX compile features - done
   -- Found OpenMP_CXX: -fopenmp (found version "4.0")
   -- Found OpenMP: TRUE (found version "4.0")
   -- Configuring done
   -- Generating done
   -- Build files have been written to: /home/user/cmake-recipes/chapter-13/recipe-02/cxx-example/build
   ```

5. 构建可执行文件：

   ```
   $ cmake --build .

   Scanning dependencies of target example
   [ 50%] Building CXX object CMakeFiles/example.dir/example.cpp.obj
   [100%] Linking CXX executable bin/example.exe
   [100%] Built target example
   ```

6. 将`example.exe`拷贝到 Windows 环境下。

7. Windows 环境下，将看到如下的输出：

   ```
   $ set OMP_NUM_THREADS=1
   $ example.exe 1000000000

   number of available processors: 2
   number of threads: 1
   we will form sum of numbers from 1 to 1000000000
   sum: 500000000500000000
   elapsed wall clock time: 2.641 seconds

   $ set OMP_NUM_THREADS=2
   $ example.exe 1000000000

   number of available processors: 2
   number of threads: 2
   we will form sum of numbers from 1 to 1000000000
   sum: 500000000500000000
   elapsed wall clock time: 1.328 seconds
   ```

8. 正如我们所看到的，二进制文件可以在 Windows 上工作，而且由于 OpenMP 并行化，我们可以观察到加速效果!

## 工作原理

我们已经成功地使用一个简单的工具链进行交叉编译了一个可执行文件，并可以在 Windows 平台上并行执行。我们可以通过设置`OMP_NUM_THREADS`来指定 OpenMP 线程的数量。从一个线程到两个线程，我们观察到运行时从 2.6 秒减少到 1.3 秒。有关工具链文件的讨论，请参阅前面的示例。

## 更多信息

可以交叉编译一组目标平台(例如：Android)，可以参考：https://cmake.org/cmake/help/latest/manual/cmake-toolchains.7.html
