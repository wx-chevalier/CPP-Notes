# 构建文档

本章的主要内容有：

- 使用 Doxygen 构建文档
- 使用 Sphinx 构建文档
- 结合 Doxygen 和 Sphinx

文档在所有的软件项目都是有必要的：对于用户来说，了解如何获得并构建代码，并且如何有效地使用源代码或库；对于开发人员来说，文档可用来描述你源码细节，并帮助其他程序员参与其中为该项目作出贡献。本章将展示如何使用 CMake 构建代码文档，这里使用了两个流行的文档框架：Doxygen 和 Sphinx。

# 12.1 使用 Doxygen 构建文档

**NOTE**:_此示例代码可以在 https://github.com/dev-cafe/cmake-cookbook/tree/v1.0/chapter-12/recipe-01 中找到，其中包含一个 C++示例。该示例在 CMake 3.5 版(或更高版本)中是有效的，并且已经在 GNU/Linux、macOS 和 Windows 上进行过测试。_

Doxygen([http://www.doxygen.nl](http://www.doxygen.nl/) )是非常流行的源代码文档工具。可以在代码中添加文档标记作为注释，而后运行 Doxygen 提取这些注释，并以 Doxyfile 配置文件中定义的格式创建文档。Doxygen 可以输出 HTML、XML，甚至 LaTeX 或 PDF。本示例将展示，如何使用 CMake 来构建 Doxygen 文档。

## 准备工作

使用前几章中介绍的消息库的简化版本。目录结构如下:

```
.
├── cmake
│    └── UseDoxygenDoc.cmake
├── CMakeLists.txt
├── docs
│    ├── Doxyfile.in
│    └── front_page.md
└── src
    ├── CMakeLists.txt
    ├── hello-world.cpp
    ├── Message.cpp
    └── Message.hpp
```

我们仍然在`src`子目录下放置源代码，并且在 CMake 子目录中有自定义的 CMake 模块。由于重点是文档，所以消除了对 UUID 的依赖，并简化了源代码。最大的区别是头文件中的大量代码注释：

```
#pragma once

#include <iosfwd>
#include <string>

/ * ! \file Message.hpp * /

/*! \class Message
* \brief Forwards string to screen
* \author Roberto Di Remigio
* \date 2018
* /

class Message {
public:
  /*! \brief Constructor from a string
  * \param[in] m a message
  */
  Message(const std::string &m) : message_(m) {}
  /*! \brief Constructor from a character array
  * \param[in] m a message
  */
  Message(const char * m): message_(std:: string(m)){}

  friend std::ostream &operator<<(std::ostream &os, Message &obj) {
    return obj.printObject(os);
  }
private:
  /*! The message to be forwarded to screen */
  std::string message_;
  /*! \brief Function to forward message to screen
  * \param[in, out] os output stream
  */
  std::ostream &printObject(std::ostream &os);
};
```

这些注释的格式是`/*!*/`，并包含一些 Doxygen 可以理解的特殊标记(参见http://www.stack.nl/~dimitri/Doxygen/manual/docblocks.html )。

## 具体实施

首先，来看看根目录下的`CMakeLists.txt`：

1. 我们声明了一个 C++11 项目：

   ```
   cmake_minimum_required(VERSION 3.5 FATAL_ERROR)
   project(recipe-01 LANGUAGES CXX)
   set(CMAKE_CXX_STANDARD 11)
   set(CMAKE_CXX_EXTENSIONS OFF)
   set(CMAKE_CXX_STANDARD_REQUIRED ON)
   ```

2. 为动态库和静态库，以及可执行文件定义了输出目录：

   ```
   include(GNUInstallDirs)
   set(CMAKE_ARCHIVE_OUTPUT_DIRECTORY
   	${CMAKE_BINARY_DIR}/${CMAKE_INSTALL_LIBDIR})
   set(CMAKE_LIBRARY_OUTPUT_DIRECTORY
   	${CMAKE_BINARY_DIR}/${CMAKE_INSTALL_LIBDIR})
   set(CMAKE_RUNTIME_OUTPUT_DIRECTORY
   	${CMAKE_BINARY_DIR}/${CMAKE_INSTALL_BINDIR})
   ```

3. 将`cmake`子目录追加到`CMAKE_MODULE_PATH`。这是需要 CMake 找到我们的自定义模块：

   ```
   list(APPEND CMAKE_MODULE_PATH "${CMAKE_SOURCE_DIR}/cmake")
   ```

4. `UseDoxygenDoc.cmake`自定义模块。将在后面讨论它的内容:

   ```
   include(UseDoxygenDoc)
   ```

5. 然后添加`src`子目录:

   ```
   add_subdirectory(src)
   ```

`src`子目录中的`CMakeLists.txt`文件包含以下构建块:

1. 添加了消息库:

   ```
   add_library(message STATIC
     Message.hpp
     Message.cpp
     )
   ```

2. 然后，声明`add_doxygen_doc`函数。这个函数可以理解这些参数：BUILD_DIR、DOXY_FILE、TARGET_NAME 和 COMMENT。使用`cmake_parse_arguments`标准 CMake 命令解析这些参数：

   ```
   function(add_doxygen_doc)
     set(options)
     set(oneValueArgs BUILD_DIR DOXY_FILE TARGET_NAME COMMENT)
     set(multiValueArgs)

     cmake_parse_arguments(DOXY_DOC
       "${options}"
       "${oneValueArgs}"
       "${multiValueArgs}"
       ${ARGN}
     )

     # ...
   endfunction()
   ```

3. Doxyfile 包含用于构建文档的所有 Doxygen 设置。一个模板`Doxyfile.in`文件作为函数参数`DOXY_FILE`传递，并解析为`DOXY_DOC_DOXY_FILE`变量。使用如下方式，配置模板文件`Doxyfile.in`:

   ```
   configure_file(
     ${DOXY_DOC_DOXY_FILE}
     ${DOXY_DOC_BUILD_DIR}/Doxyfile
     @ONLY
     )
   ```

4. 然后，定义了一个名为`DOXY_DOC_TARGET_NAME`的自定义目标，它将使用 Doxyfile 中的设置执行 Doxygen，并在`DOXY_DOC_BUILD_DIR`中输出结果:

   ```
   add_custom_target(${DOXY_DOC_TARGET_NAME}
     COMMAND
       ${DOXYGEN_EXECUTABLE} Doxyfile
     WORKING_DIRECTORY
       ${DOXY_DOC_BUILD_DIR}
     COMMENT
       "Building ${DOXY_DOC_COMMENT} with Doxygen"
     VERBATIM
     )
   ```

5. 最后，为用户打印一条状态信息:

   ```
   message(STATUS "Added ${DOXY_DOC_TARGET_NAME} [Doxygen] target to build documentation")
   ```

可以像往常一样配置项目：

```
$ mkdir -p build
$ cd build
$ cmake ..
$ cmake --build .
```

可以通过调用自定义文档目标来构建文档:

```
$ cmake --build . --target docs
```

您将注意到构建树中出现了一个`_build`子目录。它包含 Doxygen 从源文件生成的 HTML 文档。用浏览器打开`index.html`将显示 Doxygen 欢迎页面。

## 工作原理

默认情况下，CMake 不支持文档构建。但是，我们可以使用`add_custom_target`执行任意操作。需要注意的是，需要确保构建文档所需的工具(本例中是 Doxygen 和 Perl)在系统上可用。

此外，请注意`UseDoxygenDoc.cmake`自定义模块只做以下工作:

- 执行对 Doxygen 和 Perl 可执行程序的搜索
- 定义函数

使用`add_doxygen_doc`函数对文档目标进行创建。这个显式模式要优于隐式模式，我们也认为这是很好的实践方式：不要使用模块来执行类似宏(或函数)的操作。

为了限制变量定义的范围和可能出现的副作用，我们使用函数而不是宏实现了`add_doxygen_doc`。这种情况下，函数和宏都可以工作(并且会产生相同的结果)，但是建议优先使用函数而不是宏，除非需要修改父范围中的变量。

**NOTE**:_在 cmake 3.9 中添加了`FindDoxygen.cmake`模块。实现了`doxygen_add_docs`函数，其行为与我们在本示例中给出的宏类似。要了解更多细节，请访问https://cmake.org/cmake/help/v3.9/module/FindDoxygen.html 查看在线文档。_

# 12.2 使用 Sphinx 构建文档

**NOTE**:_此示例代码可以在 https://github.com/dev-cafe/cmake-cookbook/tree/v1.0/chapter-12/recipe-02 中找到，其中包含一个 C++示例。该示例在 CMake 3.5 版(或更高版本)中是有效的，并且已经在 GNU/Linux、macOS 和 Windows 上进行过测试。_

Sphinx 是一个 Python 程序，也是一个非常流行的文档系统([http://www.sphinx-doc.org](http://www.sphinx-doc.org/) )。当与 Python 项目一起使用时，可以为`docstring`解析源文件，并自动为函数和类生成文档页面。然而，Sphinx 不仅限于 Python，还可以解析 reStructuredText、Markdown，并生成 HTML、ePUB 或 PDF 文档。还有在线阅读服务([https://readthedocs.org](https://readthedocs.org/) )，它提供了一种快速编写和部署文档的方法。本示例将向您展示，如何使用 CMake 构建 Sphinx 文档。

## 准备工作

我们希望建立一个简单的网站，记录我们的消息库输出的信息。源码树现在看起来如下:

```
.
├── cmake
│    ├── FindSphinx.cmake
│    └── UseSphinxDoc.cmake
├── CMakeLists.txt
├── docs
│    ├── conf.py.in
│    └── index.rst
└── src
    ├── CMakeLists.txt
    ├── hello-world.cpp
    ├── Message.cpp
    └── Message.hpp
```

`cmake`子目录中有一些自定义模块，`docs`子目录以纯文本 reStructuredText 格式的网站主页，`index.rst`和一个带有 Sphinx 的设置 Python 模板文件`conf.py.in`，这个模板文件可以使用`sphinx-quickstart`程序自动生成。

## 具体实施

与之前的示例相比，我们将修改主`CMakeLists.txt`文件，并实现一个函数(`add_sphinx_doc`):

1. 将`cmake`文件夹附加到`CMAKE_MODULE_PATH`之后，我们将包括`UseSphinxDoc.cmake`自定义模块:

   ```
   list(APPEND CMAKE_MODULE_PATH "${CMAKE_SOURCE_DIR}/cmake")
   include(UseSphinxDoc)
   ```

2. `UseSphinxDoc.cmake`模块定义了`add_sphinx_doc`函数。我们使用关键字参数调用这个函数，以便对 Sphinx 文档的构建进行设置。自定义文档目标将称为`docs`:

   ```
   add_sphinx_doc(
     SOURCE_DIR
     	${CMAKE_CURRENT_SOURCE_DIR}/docs
     BUILD_DIR
     	${CMAKE_CURRENT_BINARY_DIR}/_build
     CACHE_DIR
     	${CMAKE_CURRENT_BINARY_DIR}/_doctrees
     HTML_DIR
     	${CMAKE_CURRENT_BINARY_DIR}/sphinx_html
     CONF_FILE
     	${CMAKE_CURRENT_SOURCE_DIR}/docs/conf.py.in
     TARGET_NAME
     	docs
     COMMENT
     	"HTML documentation"
     )
   ```

`UseSphinxDoc.cmake`模块遵循相同的显式方式，这样的使用方式要优于在前一个示例中的隐式方式：

1. 需要找到 Python 解释器和 Sphinx 可执行文件，如下:

   ```
   find_package(PythonInterp REQUIRED)
   find_package(Sphinx REQUIRED)
   ```

2. 然后，用一个值关键字参数定义`add_sphinx_doc`函数，并用`cmake_parse_arguments`解析：

   ```
   function(add_sphinx_doc)
     set(options)
     set(oneValueArgs
       SOURCE_DIR
       BUILD_DIR
       CACHE_DIR
       HTML_DIR
       CONF_FILE
       TARGET_NAME
       COMMENT
       )

     set(multiValueArgs)

     cmake_parse_arguments(SPHINX_DOC
       "${options}"
       "${oneValueArgs}"
       "${multiValueArgs}"
       ${ARGN}
       )

     # ...

   endfunction()
   ```

3. 模板文件`conf.py.in`作为`CONF_FILE`关键字参数传递，在`SPHINX_DOC_BUILD_DIR`中配置为`conf.py`：

   ```
   configure_file(
     ${SPHINX_DOC_CONF_FILE}
     ${SPHINX_DOC_BUILD_DIR}/conf.py
     @ONLY
     )
   ```

4. 添加了一个名为`SPHINX_DOC_TARGET_NAME`的自定义目标，用 Sphinx 来编排文档构建:

   ```
   add_custom_target(${SPHINX_DOC_TARGET_NAME}
     COMMAND
       ${SPHINX_EXECUTABLE}
         -q
         -b html
         -c ${SPHINX_DOC_BUILD_DIR}
         -d ${SPHINX_DOC_CACHE_DIR}
         ${SPHINX_DOC_SOURCE_DIR}
         ${SPHINX_DOC_HTML_DIR}
     COMMENT
       "Building ${SPHINX_DOC_COMMENT} with Sphinx"
     VERBATIM
     )
   ```

5. 最后，打印一条状态信息:

   ```
   message(STATUS "Added ${SPHINX_DOC_TARGET_NAME} [Sphinx] target to build documentation")
   ```

6. 配置项目并构建了文档目标:

   ```
   $ mkdir -p build
   $ cd build
   $ cmake ..
   $ cmake --build . --target docs
   ```

这将生成`SPHINX_DOC_HTML_DIR`中的 HTML 文档 生成树的子目录。

## 工作原理

我们利用`add_custom_target`的功能，可以向构建系统添加任意的构建目标。本例中，文档将使用 Sphinx 构建。由于 Sphinx 是一个可以与其他 Python 模块一起扩展的 Python 程序，所以`docs`目标将依赖于 Python 解释器。我们使用`find_package`确保依赖关系。需要注意的是，`FindSphinx.cmake`模块还不是一个标准的 CMake 模块；它的副本包含在项目源代码中，位于`cmake`子目录下。

# 12.3 结合 Doxygen 和 Sphinx

**NOTE**:_此示例代码可以在 https://github.com/dev-cafe/cmake-cookbook/tree/v1.0/chapter-12/recipe-03 中找到，其中包含一个 C++示例。该示例在 CMake 3.5 版(或更高版本)中是有效的，并且已经在 GNU/Linux、macOS 和 Windows 上进行过测试。_

我们有一个 C++项目，因此 Doxygen 是生成源代码文档的理想选择。然而，我们也希望发布面向用户的文档，例如：介绍设计选择。所以我们想使用 Sphinx，因为生成的 HTML 也可以在移动设备上查看，而且可以部署文档进行在线阅读([https://readthedocs.org](https://readthedocs.org/) )。本教程将演示如何使用 Breathe 插件([https://breathe.readthedocs.io](https://breathe.readthedocs.io/) )组合 Doxygen 和 Sphinx。

## 准备工作

这个示例的目录结构，类似于之前的两个示例:

```
.
├── cmake
│    ├── FindPythonModule.cmake
│    ├── FindSphinx.cmake
│    └── UseBreathe.cmake
├── CMakeLists.txt
├── docs
│    ├── code-reference
│    │    ├── classes-and-functions.rst
│    │    └── message.rst
│    ├── conf.py.in
│    ├── Doxyfile.in
│    └── index.rst
└── src
    ├── CMakeLists.txt
    ├── hello-world.cpp
    ├── Message.cpp
    └── Message.hpp
```

`docs`子目录现在同时包含一个`Doxyfile.in`和一个`conf.py.in`模板文件。模板文件中，分别设置了 Doxygen 和 Sphinx。此外，还有一个`code-referenc`子目录。

`code-referenc`子目录中的文件包含 Breathe 指令，用来在 Sphinx 中包含 doxygen 生成的文档：

```
Messaging classes
=================
Message
-------
..  doxygenclass:: Message
    :project: recipe-03
    :members:
    :protected-members:
    :private-members:
```

这将输出 Message 类的文档。

## 具体实施

`src`目录中的`CMakeLists.txt`文件没有改变。主`CMakeLists.txt`文件中有修改：

1. 包含`UseBreathe.cmake`自定义模块：

   ```
   list(APPEND CMAKE_MODULE_PATH "${CMAKE_SOURCE_DIR}/cmake")
   include(UseBreathe)
   ```

2. 调用`add_breathe_doc`函数，这个函数是在自定义模块中定义的，它接受关键字参数，来设置 Doxygen 和 Sphinx：

   ```
   add_breathe_doc(
     SOURCE_DIR
       ${CMAKE_CURRENT_SOURCE_DIR}/docs
     BUILD_DIR
       ${CMAKE_CURRENT_BINARY_DIR}/_build
     CACHE_DIR
       ${CMAKE_CURRENT_BINARY_DIR}/_doctrees
     HTML_DIR
       ${CMAKE_CURRENT_BINARY_DIR}/html
     DOXY_FILE
       ${CMAKE_CURRENT_SOURCE_DIR}/docs/Doxyfile.in
     CONF_FILE
       ${CMAKE_CURRENT_SOURCE_DIR}/docs/conf.py.in
     TARGET_NAME
       docs
     COMMENT
       "HTML documentation"
     )
   ```

让我们看一下`UseBreatheDoc.cmake`模块，其遵循了与我们在前两个示例中描述的显式模式。具体描述如下:

1. 文档生成依赖于 Doxygen:

   ```
   find_package(Doxygen REQUIRED)
   find_package(Perl REQUIRED)
   ```

2. 还依赖于 Python 解释器和 Sphinx:

   ```
   find_package(PythonInterp REQUIRED)
   find_package(Sphinx REQUIRED)
   ```

3. 此外，还必须找到 breathe 的 Python 模块。这里，我们使用`FindPythonModule.cmake`模块:

   ```
   include(FindPythonModule)
   find_python_module(breathe REQUIRED)
   ```

4. 定义了`add_breathe_doc`函数，这个函数有一个单值关键字参数，我们将使用`cmake_parse_arguments`命令解析它:

   ```
   function(add_breathe_doc)
     set(options)
     set(oneValueArgs
       SOURCE_DIR
       BUILD_DIR
       CACHE_DIR
       HTML_DIR
       DOXY_FILE
       CONF_FILE
       TARGET_NAME
       COMMENT
       )
     set(multiValueArgs)

     cmake_parse_arguments(BREATHE_DOC
       "${options}"
       "${oneValueArgs}"
       "${multiValueArgs}"
       ${ARGN}
       )

     # ...

   endfunction()
   ```

5. `BREATHE_DOC_CONF_FILE`中的 Sphinx 模板文件，会通过`conf.py`配置到的`BREATHE_DOC_BUILD_DIR`目录下：

   ```
   configure_file(
     ${BREATHE_DOC_CONF_FILE}
     ${BREATHE_DOC_BUILD_DIR}/conf.py
     @ONLY
     )
   ```

6. 相应地，Doxygen 的`BREATHE_DOC_DOXY_FILE`模板文件配置为`BREATHE_DOC_BUILD_DIR`中的 Doxyfile:

   ```
   configure_file(
     ${BREATHE_DOC_DOXY_FILE}
     ${BREATHE_DOC_BUILD_DIR}/Doxyfile
     @ONLY
     )
   ```

7. 添加`BREATHE_DOC_TARGET_NAME`自定义目标。注意，只有 Sphinx 在运行时，对 Doxygen 的调用才发生在`BREATHE_DOC_SPHINX_FILE`中:

   ```
   add_custom_target(${BREATHE_DOC_TARGET_NAME}
     COMMAND
       ${SPHINX_EXECUTABLE}
         -q
         -b html
         -c ${BREATHE_DOC_BUILD_DIR}
         -d ${BREATHE_DOC_CACHE_DIR}
         ${BREATHE_DOC_SOURCE_DIR}
         ${BREATHE_DOC_HTML_DIR}
     COMMENT
       "Building ${BREATHE_DOC_TARGET_NAME} documentation with Breathe, Sphinx and Doxygen"
     VERBATIM
     )
   ```

8. 最后，打印一条状态信息:

   ```
   message(STATUS "Added ${BREATHE_DOC_TARGET_NAME} [Breathe+Sphinx+Doxygen] target to build documentation")
   ```

9. 配置完成后，构建文档:

   ```
   $ mkdir -p build
   $ cd build
   $ cmake ..
   $ cmake --build . --target docs
   ```

该文档将在`BREATHE_DOC_HTML_DIR`子目录中可用。

## 工作原理

尽管在声明定制的`BREATHE_DOC_TARGET_NAME`目标时只调用了 Sphinx，但这里 Doxygen 和 Sphinx 都在运行。这要感谢 Sphinx 的`conf.py`文件中的以下设置:

```
def run_doxygen(folder):
  """Run the doxygen make command in the designated folder"""

  try:
      retcode = subprocess.call("cd {}; doxygen".format(folder), shell=True)
    if retcode < 0:
      sys.stderr.write(
      "doxygen terminated by signal {}".format(-retcode))
  except OSError as e:
    sys.stderr.write("doxygen execution failed: {}".format(e))


def setup(app):
  run_doxygen('@BREATHE_DOC_BUILD_DIR@')
```

Doxygen 将生成 XML 输出，Breathe 插件将能够与所选择的 Sphinx 文档样式一致的形式，呈现 XML 输出。
